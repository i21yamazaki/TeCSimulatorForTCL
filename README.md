# TeCSimulatorForTCL

[![CI Tests](https://github.com/i21yamazaki/TeCSimulatorForTCL/actions/workflows/test.yml/badge.svg)](https://github.com/i21yamazaki/TeCSimulatorForTCL/actions/workflows/test.yml)

TeC制御言語（TCL）を用いて TeC のシミュレーションを行うためのシミュレータです。

## 実行環境

UNIX（macOS など）、またはそれに準ずるOS（Linux・FreeBSD 等）を想定しています。  
macOS と Ubuntu でのみ動作確認がされています。

C++用コンパイラとして、`g++`コマンドで実行できるGCC（または Apple Clang 等）がインストールされている必要があります。

## インストール

以下のコマンドを順に実行することで、インストールできます。

最後の行の、`sudo make install`ではパスワードが要求される場合があります。  
環境によっては`sudo`が必要ない場合もあります。

```shell
git clone https://github.com/i21yamazaki/TeCSimulatorForTCL
cd TeCSimulatorForTCL
make all
sudo make install
```

## アセンブラ

以下のコマンドでアセンブルを行います。

```shell
tasm <program>.t7
```

アセンブルが成功すると、`<program>.bin` と `<program>.nt` が生成されます。

`<program>.bin` は、機械語ファイルです。

`<program>.nt` は、名前表ファイルです。

## シミュレータ

以下のコマンドでシミュレーションを行います。

名前表ファイルは指定しなくてもシミュレーションは実行できますが、
その場合は、ラベル名を参照できません。

```shell
tec <program>.bin [<program>.nt]
```

シミュレータは、標準入力からTCLによるシミュレーション手順を受け取ります。

## TeC制御言語

TeCのコンソールパネルによる操作を記述することができます。
テストケースの入力として与え、シミュレーション時に利用します。

以下の拡張BNFは、TeC制御言語の文法を表しています。

```BNF
<操作記述>              ::= {<行>} [<終了記述>] EOF
<行>                    ::= <値変更行> | <命令行> | <空行> 
<値変更行>              ::= <値変更> [<コメント>] <改行>
<値変更>                ::= <レジスタ値変更> | <フラグ値変更> | <主記憶値変更>
<レジスタ値変更>        ::= <レジスタ> = <バイト値>
<フラグ値変更>          ::= <フラグ> = <ビット値>
<主記憶値変更>          ::= <アドレス> = <バイト値>
<命令行>                ::= '$' <命令記述> [<コメント>] <改行>
<空行>                  ::= [<コメント>] <改行>
<終了記述>              ::= '$' END {- EOF}
<命令記述>              ::= <単純命令> | <引数付き命令>
<単純命令>              ::= RUN | STOP | RESET | WRITE
<引数付き命令>          ::= <待機命令> | <表示命令> | <シリアル命令> | <シリアルモード命令> | <表示モード命令> | <DATA-SW操作命令> | <パラレル書き込み命令> | <アナログ書き込み命令>
<待機命令>              ::= WAIT <待機条件>
<待機条件>              ::= <待機時間> | STOP | SERIAL
<待機時間>              ::= <時間単位> <10進数値>
<時間単位>              ::= STATES | SEC | MS
<DATA-SW操作命令>       ::= DATA-SW <バイト値>
<パラレル書き込み命令>  ::= PARALLEL <バイト値>
<アナログ書き込み命令>  ::= ANALOG <ADCチャンネル> <電圧値>
<ADCチャンネル>         ::= CH0 | CH1 | CH2 | CH3
<電圧値>                ::= <実数値> <電圧単位>
<電圧単位>              ::= V | mV
<実数値>                ::= <10進数値> | <10進数値> '.' <10進数値>
<表示命令>              ::= PRINT <表示対象>
<表示対象>              ::= <レジスタ> | <フラグ> | <アドレス> | PARALLEL | EXT-PARALLEL | BUZ | SPK | RUN
<シリアル命令>          ::= SERIAL <バイト列>
<シリアルモード命令>    ::= SERIAL-MODE <出力モード>
<表示モード命令>        ::= PRINT-MODE <出力モード>
<出力モード>            ::= RAW | HEX | TEC | SDEC | UDEC
<バイト列>              ::= <バイト列要素> {',' <バイト列要素>}
<バイト列要素>          ::= <文字列定数> | <バイト値>
<文字列定数>            ::= '"' <表示文字> '"'
<文字定数>              ::= '\'' <表示文字> '\''
<バイト値>              ::= <式>
<式>                    ::= <項> {<加減記号> <項>}
<項>                    ::= <因子> {<乗除記号> <因子>}
<因子>                  ::= [<符号記号>] <符号なし因子>
<符号なし因子>          ::= <数値> | <ラベル> | '(' <式> ')'
<ラベル>                ::= <ラベル開始文字> {<ラベル文字>}
<ラベル開始文字>        ::= <英字> | '_'
<ラベル文字>            ::= <ラベル開始文字> | <10進数字>
<英字>                  ::= A | B | C | D | E | F | G | H | I | J | K | L | M | N | O | P | Q | R | S | T | U | V | W | X | Y | Z
<アドレス>              ::= '[' <バイト値> ']'
<符号記号>              ::= '+' | '-'
<加減記号>              ::= '+' | '-'
<乗除記号>              ::= '*' | '/'
<レジスタ>              ::= G0 | G1 | G2 | SP | PC
<フラグ>                ::= C | S | Z
<ビット値>              ::= 0 | 1
<10進数字>              ::= 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9
<16進数字>              ::= <10進数字> | A | B | C | D | E | F
<数値>                  ::= <10進数値> | <16進数値>
<10進数値>              ::= {<10進数字>}
<16進数値>              ::= <10進数字> {<16進数字>} H
<改行>                  ::= '\n'
<コメント>              ::= ';' {- '\n'}
```

### コメント

';' から行末まではすべてコメントとなり無視されます。

### レジスタ値変更

G0, G1, G2, SP, PC の5つのレジスタについて、値を変更できます。

指定できる値は、10進数、16進数、及びラベルによる四則演算で、
これは、TeCのアセンブリ言語の文法と同じになっています。

例
```
G0 = 10                     ; G0 <- 10
G1 = 10H                    ; G1 <- 11H
G2 = (1 + 2) * (3 - 4) / 5  ; G2 <- (1 + 2) * (3 - 4) / 5
SP = 0DCH                   ; SP <- 0DCH
PC = START                  ; PC <- START
```

### 主記憶値変更

主記憶内の値を変更できます。

指定できる値は、10進数、16進数、及びラベルによる四則演算で、
これは、TeCのアセンブリ言語の文法と同じになっています。

ただし、IPLの格納されたROM領域（0E0H番地以降）については変更できません。もし、変更しようとしても単純に無視されます。

例
```
[10H]   = 10                ; [10H] <- 10
[A]     = 30H               ; [A] <- 30H
[A + 5] = B                 ; [A + 5] <- B
[0FFH]  = 0                 ; ROMへの書き込みは無視される
```

### フラグ値変更

C, S, Z の3つのフラグレジスタについて、値を変更できます。

指定できる値は、`0` or `1` のいずれかです。

使用例
```
C = 0                       ; C <- 0
S = 1                       ; S <- 1
Z = 0                       ; Z <- 0
```

### 命令

'$' から始まる行のほとんどは命令行となります。

使用可能な命令は、以下のとおりです。

| 命令        | 操作                                                     |
| ----------- | -------------------------------------------------------- |
| RUN         | 実行開始                                                 |
| STOP        | 実行停止                                                 |
| RESET       | 初期化                                                   |
| WAIT        | 条件を満たすまでシミュレーションを行い待機               |
| PRINT       | レジスタ・フラグ・主記憶・出力装置などの値を出力         |
| PRINT-MODE  | レジスタ・フラグ・主記憶・出力装置などの値の出力形式設定 |
| SERIAL      | シリアル入力への書き込み                                 |
| SERIAL-MODE | シリアル出力の出力形式設定                               |
| DATA-SW     | データスイッチの値変更                                   |
| WRITE       | コンソール割り込みの発生                                 |
| ANALOG      | アナログ入力                                             |
| PARALLEL    | パラレル入力                                             |

#### RUN

この命令は、実行開始を表します。
コンソールパネルのRUNボタンに相当します。

シミュレーションでは、単に実行状態が切り替わるだけであり、
実際の機械語命令の実行は、WAIT命令などが評価されるまで遅延されます。

例
```
$RUN            ; 実行開始
```

#### STOP

この命令は、実行停止を表します。
コンソールパネルのSTOPボタンに相当します。

例
```
$STOP           ; 実行停止
```

#### RESET

この命令は、初期化を表します。
コンソールパネルのRESETボタンに相当します。

例
```
$RESET          ; リセット
```

#### WAIT

この命令は、次のいずれかのオプションを使用し、以降の命令実行を遅延させます。

##### STOP

このオプションを指定すると、実行状態の間シミュレーションを行い、実行終了を待ちます。

シミュレーション中にエラーが発生した場合は、エラーメッセージを出力し、エラーコード1で終了します。

すでに実行が終了している場合は、単に無視されます。

使用例
```
$WAIT STOP          ; 実行停止まで待機
```

##### STATES

このオプションを指定すると、実行状態の間、指定したステート数が経過するまで待機します。

ステート数は、10進数で指定する必要があります。

指定したステート数が経過するまでに実行が終了した場合や、既に実行が終了している場合はそれ以上のシミュレーションは行いません。

シミュレーションは命令単位で行われるため、実際に実行されるステート数は、指定した値より大きくなる可能性があります。

例
```
$WAIT STATES 256    ; 256ステート経過するまで待機
```

##### SEC

このオプションを指定すると、実行状態の間、（シミュレーション時間で）指定した秒数が経過するまで、シミュレーションを行います。

ステート数は、10進数で指定する必要があります。

指定した秒数が経過するまでに、実行が終了した場合や、既に実行が終了している場合はそれ以上のシミュレーションは行いません。

例
```
$WAIT SEC 10        ; 10秒経過するまで待機
```

##### MS

このオプションを指定すると、実行状態の間、（シミュレーション時間で）指定したミリ秒数が経過するまで、シミュレーションを行います。

ステート数は、10進数で指定する必要があります。

指定したミリ秒数が経過するまでに、実行が終了した場合や、既に実行が終了している場合はそれ以上のシミュレーションは行いません。

例
```
$WAIT MS 500        ; 500 ms (= 0.5 s) 経過するまで待機
```

##### SERIAL

このオプションを指定すると、実行状態の間、シリアル入力がすべて受け取られる（IN命令による読み取りが完了する）まで、シミュレーションを行います。

シリアル入力がすべて受け取られるまでに、実行が終了した場合や、既に実行が終了している場合はそれ以上のシミュレーションは行いません。

例
```
$WAIT SERIAL        ; シリアル入力が全て受け取られるまで待機
```

#### DATA-SW

この命令は、データスイッチの値を変更します。

値は、10進数、16進数、文字定数、及びラベルによる四則演算で、これは、TeCのアセンブリ言語の文法と同じです。

例
```
$DATA-SW 0FFH       ; データスイッチの値を 0FFH に設定
                    ;（データスイッチの全てのトグルスイッチを倒す）
```

#### WRITE

この命令は、書き込みボタンによるコンソール割り込みを発生させます。

シミュレーションでは、実行状態が実行中でない場合は、エラーが発生します。

例
```
$WRITE              ; コンソール割り込み
```

#### PRINT

この命令を使用することで、以下の値を出力することができます。
* レジスタ（PCも含む）
* フラグ
* 主記憶
* パラレル出力
* 拡張パラレル出力
* ブザー
* スピーカ
* 実行状態（RUNランプ）

出力される値は、この命令が実行された時点でのプリントモードに依存します。

レジスタの場合は、`G0`, `G1`, `G2`, `SP`, `PC` のいずれかを指定します。

フラグの場合は、`C`, `S`, `Z` のいずれかを指定します。

主記憶の場合は、`[アドレス]` を指定します。
アドレス部は、10進数、16進数、文字定数、及びラベルによる四則演算で、これは、TeCのアセンブリ言語の文法と同じです。

パラレル出力を出力する場合は、`PARALLEL` を指定します。

パラレル拡張出力を出力する場合は、`EXT-PARALLEL` を指定します。

スピーカを出力する場合は、`SPK` を指定します。

ブザーを出力する場合は、`BUZ` を指定します。

実行状態を出力する場合は、`RUN` を指定します。

フラグや一部の出力で、データが8ビットに満たない場合は、
上位ビットが0で埋められているものとします。

例
```
$PRINT G0            ; G0の値を出力
$PRINT C             ; Cフラグの値を出力
$PRINT [10H]         ; 主記憶の 10H番地 に格納されている値を出力
$PRINT SPK           ; スピーカの値を出力
$PRINT BUZ           ; ブザーの値を出力
$PRINT RUN           ; RUNランプの値を出力
$PRINT PARALLEL      ; パラレル出力の値を出力
$PRINT EXT-PARALLEL  ; 拡張パラレル出力の値を出力 
```

#### PRINT-MODE

この命令は、レジスタ・フラグ・主記憶の値を出力する際の書式を指定します。
書式には、`RAW`, `HEX`, `TEC`, `SDEC`, `UDEC` があります。
初期値は、`UDEC` です。

例
```
$PRINT-MODE RAW ; RAWモードに設定
```

#### SERIAL

この命令は、TeCのシリアル入力への書き込みを行います。

指定できる値は、10進数、16進数、文字定数、及びラベルによる四則演算 と 文字列定数 です。
値は、カンマ区切りで複数指定でき、これは、TeCのアセンブリ言語の文法と同じです。

例
```
$SERIAL "Hello, TeC.", 0AH  ; シリアル入力に "Hello, TeC." と改行コードを書き込む
```

#### SERIAL-MODE

この命令は、TeCのシリアル出力の値を出力する際の書式を指定します。
書式には、`RAW`, `HEX`, `TEC`, `SDEC`, `UDEC` があります。
初期値は、`RAW`です。

例
```
$SERIAL-MODE HEX ;16進数モードに設定。
```

### ANALOG

この命令は、TeCのアナログ入力を操作します。

ADCチャンネルと電圧値を指定する必要があります。

ADCチャンネルには、`CH0` ~ `CH3` の4つがあります。

電圧値は、10進数の実数と単位によって指定します。

単位は、Vまたは、mV です。

電圧値は、0V以上3.3V以下で指定する必要があります。

負の数を指定することはできません。
また、3.3Vを超える場合は、3.3Vになります。

例
```
$ANALOG CH0 
```

### PARALLEL

この命令は、TeCのパラレル入力を操作します。

指定できる値は、10進数、16進数、文字定数、及びラベルによる四則演算で、これは、TeCのアセンブリ言語の文法と同じです。

例
```
$PARALLEL 7EH
```

### 終了記述

`$END`は、全ての操作を終了することを表します。
命令と同様に、'$' から始まりますが、厳密には命令ではありません。

`$END` 以降の記述は全て無視されるため、コメントなどを自由に書くことができます。

### 出力モード

`$PRINT-MODE`命令や`$SERIAL-MODE`命令によって設定する出力モードには以下の種類があります。

以下に示す出力例は、値として以下の 5 byte を出力したものとします。
```
0x48, 0x65, 0x6c, 0x6c, 0x6f
```
また、RAW以外のモードについての2つ目の出力例は、以下の 10 byte を出力したものとします。
```
0x01 0x23 0x45 0x67 0x89 0xAB 0xCD 0xEF 0xFE 0xDC
```

#### RAW

読み取った値をそのまま出力します。
このモードは、値が有効な文字コードを表している場合に使用されます。

出力例
```
Hello
```

#### HEX

読み取った値を2桁の16進数（大文字）に変換し、
1 byte ごとに空白で区切り、8 byte ごとに改行して出力します。
ただし、出力する値のバイト数が 8 の倍数ではない場合、
末尾には空白ではなく改行が挿入されます。

出力例１
```
48 65 6C 6C 6F

```

出力例２
```
01 23 45 67 89 AB CD EF
FE DC

```

#### TEC

読み取った値を3桁の16新数（大文字）に変換し、
末尾に 'H' をつけて 1 byte ごとに改行して出力します。

出力例１
```
048H
065H
06CH
06CH
06FH

```

出力例２
```
001H
023H
045H
067H
089H
0ABH
0CDH
0EFH
0FEH
0DCH

```

#### SDEC

読み取った値を符号付き10進数に変換し、
改行区切りで出力します。


出力例１
```
72
101
108
108
111

```

出力例２
```
1
35
69
103
137
171
205
239
254
220

```

#### UDEC

読み取った値を符号なし10新数に変換し、
改行区切りで出力します。


出力例１
```
72
101
108
108
111

```

出力例２
```
1
35
69
103
-119
-85
-51
-17
-2
-36

```
